      *----------------------------------------------------------------*
      *                         INTERBANK                              *
      *                        COBOL ACADEMY                           *
      *                                                                *
      *  FILE-INPUT  : ENTRADA   TRANSACCIONES DIARIAS                 *
      *  FILE-OUTPUT : SALIDA    TRANSACCIONES DELIM CANAL/MARCA       *
      *----------------------------------------------------------------*
       ID DIVISION.
      *------------*
       PROGRAM-ID. CDCG1FOC.
       AUTHOR.     SILMAG01.
       DATE-WRITTEN.  AGOSTO 2025.
       ENVIRONMENT DIVISION.
      *---------------------*
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE-INPUT ASSIGN TO TXG01
               ORGANIZATION IS SEQUENTIAL
               FILE STATUS IS FS-TXG01.

           SELECT FILE-OUTPUT ASSIGN TO TXOUTG1
               ORGANIZATION IS SEQUENTIAL
               FILE STATUS IS FS-OUTG1.
       DATA DIVISION.
      *-------------.
       FILE SECTION.
       FD  FILE-INPUT RECORDING MODE IS F.
       01  REG-TRX.
           05  TS-INICIO         PIC X(26).   *> Fecha Hora Transacción
           05  CO-IDTM           PIC X(4).    *> Tipo Request
           05  CO-CONEX          PIC X(3).    *> Ruta
           05  CO-APPL           PIC X(3).    *> Aplicación
           05  FIS-TIPO          PIC X(4).    *> Tipo Response
           05  FIS-F2            PIC X(19).   *> Tarjeta
           05  FIS-F3            PIC X(6).    *> Processing Code
           05  FIS-F4            PIC X(12).   *> Monto Transacción
           05  FIS-F18           PIC X(4).    *> Merchant Type
           05  FIS-F22           PIC X(3).    *> POS Entry Mode
           05  FIS-F25           PIC X(2).    *> POS Condition Code
           05  FIS-F42           PIC X(15).   *> Código Comercio
           05  FIS-F43           PIC X(40).   *> Comercio
           05  FIS-F49           PIC X(3).    *> Moneda
           05  FIS-F124          PIC X(255).  *> Info
           05  CODUNICO          PIC X(10).   *> Código Único

       FD  FILE-OUTPUT RECORDING MODE IS F.
       01  REG-TRX-OUT           PIC X(473).

       WORKING-STORAGE SECTION.
      *=================================================================
      *                  VARIABLES DE ARCHIVO
      *=================================================================
       01 FS-TXG01           PIC 99.
       01 FS-OUTG1           PIC 99.
       01 DELIM              PIC X(2) VALUE '|^'.

       01 SW-EOF.
         05 SW-EOF-END     PIC XX VALUE 'NO'.
           88 SW-EOF-OK    VALUE 'SI'.
           88 SW-EOF-NO    VALUE 'NO'.

      *=================================================================
      *                  VARIABLES DE LOGICA
      *=================================================================

       *> MARCA AMEX/VISA
       01  WS-F2-MARCA      PIC X(1).
         88 WS-F2-IS-3-4    VALUES '3' '4'.
         88 WS-F2-IS-3      VALUES '3'.
         88 WS-F2-IS-4      VALUES '4'.

       *>Processing Code
       01 WS-F3-CODE            PIC X(2).
         88 WS-F3-10-55-56-70   VALUES '10' '55' '56' '70'.
         88 WS-F3-55-56-70      VALUES '55' '56' '70'.
         88 WS-F3-IS-10         VALUES '10'.

       *>Merchant Type
       01  WS-F18-CODE      PIC X(4).
         88 WS-F18-IS-6010  VALUES '6010'.
         88 WS-F18-IS-6011  VALUES '6011'.
         88 WS-F18-IS-4121  VALUES '4121'.

       *>POS Entry Mode
       01  WS-F22-CODE         PIC X(2).
         88 WS-F22-IS-02-05-07 VALUES '02' '05' '07'.
         88 WS-F22-IS-01-06    VALUES '01' '06'.
         88 WS-F22-IS-05-07    VALUES '05' '07'.
         88 WS-F22-IS-01-81    VALUES '01' '81'.
         88 WS-F22-IS-01       VALUES '01'.
         88 WS-F22-IS-81       VALUES '81'.
         88 WS-F22-IS-EMPTY    VALUES SPACES.

       *>POS Condition Code
       01  WS-F25-CODE          PIC X(2).
         88 WS-F25-00-01-05-59  VALUES '00' '01' '05' '59'.
         88 WS-F25-IS-00        VALUES '00'.
         88 WS-F25-IS-01        VALUES '01'.
         88 WS-F25-IS-02        VALUES '02'.
         88 WS-F25-IS-08        VALUES '08'.
         88 WS-F25-IS-40        VALUES '40'.
         88 WS-F25-IS-59        VALUES '59'.

       *>Info
       01  WS-F124-CODE      PIC X(4).
         88 WS-F124-IS-T115 VALUES 'T115'.

       *>Tipo Response
       01  WS-FIS-TIPO                 PIC X(4).
         88 WS-TIPO-0110-0130-0430  VALUES '0110' '0130' '0430'.

       01  CANAL          PIC X(20).
       01  MARCA          PIC X(10).

       01  FLAG-CONTAINS  PIC 9(02).
       01  FLAG-DUPLICATE PIC X(01) VALUE 'F'.

       01  TS-INICIO-ANT         PIC X(26) VALUE SPACES.
       01  CO-IDTM-ANT           PIC X(4)  VALUE SPACES.
       01  FIS-F2-ANT            PIC X(19) VALUE SPACES.
       01  FIS-F4-ANT            PIC X(12) VALUE SPACES.
       01  FIS-F42-ANT           PIC X(15) VALUE SPACES.
       01  FIS-F49-ANT           PIC X(3)  VALUE SPACES.

       PROCEDURE DIVISION.
      *-------------------*
           PERFORM 100-START-PROGRAM
           PERFORM 200-PROCESS-TRXS
           PERFORM 400-FINALIZE
           STOP RUN.

       100-START-PROGRAM.
      *------------------*
           INITIALIZE SW-EOF
           PERFORM OPEN-FILE-INPUT
           PERFORM OPEN-FILE-OUTPUT.

       200-PROCESS-TRXS.
      *-----------------*
           PERFORM READ-FILE-INPUT
           PERFORM 300-PROCESS-TRXS-RECORDS UNTIL SW-EOF-OK.

       300-PROCESS-TRXS-RECORDS.
      *-------------------------*
           MOVE 'F' TO FLAG-DUPLICATE
           MOVE 0 TO FLAG-CONTAINS
           INSPECT REG-TRX TALLYING FLAG-CONTAINS FOR ALL DELIM.

           IF  FLAG-CONTAINS > 0
            DISPLAY 'CHAR:' TS-INICIO  FIS-F42
           END-IF

           IF TS-INICIO=TS-INICIO-ANT AND
              CO-IDTM=CO-IDTM-ANT AND
              FIS-F2=FIS-F2-ANT AND
              FIS-F4=FIS-F4-ANT AND
              FIS-F42=FIS-F42-ANT AND
              FIS-F49=FIS-F49-ANT
              DISPLAY 'DUPLICADO:'
                       TS-INICIO
                       CO-IDTM
                       FIS-F2
                       FIS-F4
                       FIS-F42
                       FIS-F49

              MOVE 'T' TO FLAG-DUPLICATE
           END-IF

           MOVE FIS-F2 (1:1) TO WS-F2-MARCA
           MOVE FIS-TIPO  TO WS-FIS-TIPO


           IF FLAG-CONTAINS = 0
              AND FLAG-DUPLICATE = 'F'
              AND WS-F2-IS-3-4
              AND WS-TIPO-0110-0130-0430
             PERFORM CALCULAR-CANAL
             PERFORM CALCULAR-MARCA
             PERFORM WRITE-OUTPUT

           MOVE TS-INICIO TO TS-INICIO-ANT
           MOVE CO-IDTM   TO CO-IDTM-ANT
           MOVE FIS-F2    TO FIS-F2-ANT
           MOVE FIS-F4    TO FIS-F4-ANT
           MOVE FIS-F42   TO FIS-F42-ANT
           MOVE FIS-F49   TO FIS-F49-ANT

           END-IF

           PERFORM READ-FILE-INPUT.

       CALCULAR-CANAL.
      *--------------*
           MOVE SPACES TO CANAL
           MOVE FIS-F3  (1:2) TO WS-F3-CODE
           MOVE FIS-F18       TO WS-F18-CODE
           MOVE FIS-F22 (1:2) TO WS-F22-CODE
           MOVE FIS-F25       TO WS-F25-CODE
           MOVE FIS-F124(1:4) TO WS-F124-CODE

           EVALUATE TRUE
              WHEN FIS-F4 = '000000000000'          *> IMPORTE CERO
                   MOVE 'IMPORTE CERO' TO CANAL
              WHEN WS-F18-IS-6010                   *> OTROS ATM
                   OR WS-F18-IS-6011
                   MOVE 'OTROS ATM' TO CANAL
              WHEN WS-F25-IS-59                     *> ECOMMERCE 1
                   AND NOT WS-F3-10-55-56-70
                   MOVE 'ECOMMERCE' TO CANAL
              WHEN WS-F25-IS-01                     *> ECOMMERCE 2
                   AND NOT WS-F3-10-55-56-70
                   AND WS-F22-IS-01-81
                   AND WS-F2-IS-4
                   MOVE 'ECOMMERCE' TO CANAL
              WHEN WS-F25-IS-01                     *> ECOMMERCE 3
                   AND NOT WS-F3-10-55-56-70
                   AND WS-F22-IS-01
                   AND WS-F2-IS-3
                   MOVE 'ECOMMERCE' TO CANAL
              WHEN WS-F25-IS-00                     *> ECOMMERCE 4
                   AND NOT WS-F3-10-55-56-70
                   AND WS-F22-IS-01
                   AND WS-F2-IS-4
                   MOVE 'ECOMMERCE' TO CANAL
              WHEN WS-F25-IS-00                     *> ECOMMERCE 5
                   AND NOT WS-F3-10-55-56-70
                   AND WS-F22-IS-01-06
                   AND WS-F2-IS-3
                   MOVE 'ECOMMERCE' TO CANAL
              WHEN WS-F25-IS-00                     *> ECOMMERCE 6
                   AND NOT WS-F3-10-55-56-70
                   AND WS-F22-IS-81 AND WS-F2-IS-3
                   AND NOT WS-F124-IS-T115
                   MOVE 'ECOMMERCE' TO CANAL
              WHEN WS-F25-IS-00                     *> ECOMMERCE 7
                   AND NOT WS-F3-10-55-56-70
                   AND WS-F22-IS-EMPTY
                   AND WS-F18-IS-4121
                   AND WS-F2-IS-3
                   AND NOT WS-F124-IS-T115
                   MOVE 'ECOMMERCE' TO CANAL
              WHEN WS-F25-00-01-05-59               *> VISA DIRECT OCT
                   AND WS-F3-55-56-70
                   MOVE 'VISA DIRECT OCT' TO CANAL
              WHEN WS-F25-00-01-05-59               *> VISA DIRECT AFT
                   AND WS-F3-IS-10
                   MOVE 'VISA DIRECT AFT' TO CANAL
              WHEN WS-F25-IS-40                     *> RECURRENTE
                   MOVE 'RECURRENTE' TO CANAL
              WHEN WS-F25-IS-08                     *> MOTO
                   MOVE 'MOTO' TO CANAL
              WHEN WS-F25-IS-00                     *> POS1
                   AND WS-F22-IS-02-05-07
                   MOVE 'POS' TO CANAL
              WHEN WS-F25-IS-00                     *> POS2
                   AND WS-F22-IS-01-81
                   AND WS-F124-IS-T115
                   AND WS-F2-IS-3
                   MOVE 'POS' TO CANAL
              WHEN WS-F25-IS-02                     *> POS3
                   AND WS-F22-IS-05-07
                   MOVE 'POS' TO CANAL
              WHEN OTHER
                   MOVE 'OTROS' TO CANAL
           END-EVALUATE.

       CALCULAR-MARCA.
      *--------------*
           EVALUATE FIS-F2  (1:1)
            WHEN '3' MOVE 'AMEX' TO MARCA
            WHEN '4' MOVE 'VISA' TO MARCA
            WHEN OTHER MOVE SPACES TO MARCA
           END-EVALUATE.

       OPEN-FILE-INPUT.
      *---------------*
           OPEN INPUT FILE-INPUT
           IF (FS-TXG01  > "07")
            DISPLAY "ERR ABRIR ARCH ENTRADA" FS-TXG01
            STOP RUN
           END-IF.

       READ-FILE-INPUT.
      *---------------*
           READ FILE-INPUT AT END SET SW-EOF-OK TO TRUE END-READ.

       OPEN-FILE-OUTPUT.
      *----------------*
           OPEN OUTPUT  FILE-OUTPUT
           IF (FS-OUTG1   > "07")
            DISPLAY "ERR ABRIR ARCH SALIDA" FS-OUTG1
            STOP RUN
           END-IF.

       WRITE-OUTPUT.
      *------------*
           STRING
           TS-INICIO DELIM
           CO-IDTM   DELIM
           CO-CONEX  DELIM
           CO-APPL   DELIM
           FIS-TIPO  DELIM
           FIS-F2    DELIM
           FIS-F3    DELIM
           FIS-F4    DELIM
           FIS-F18   DELIM
           FIS-F22   DELIM
           FIS-F25   DELIM
           FIS-F42   DELIM
           FIS-F43   DELIM
           FIS-F49   DELIM
           FIS-F124  DELIM
           CODUNICO  DELIM
           CANAL     DELIM
           MARCA
           DELIMITED BY SIZE
           INTO REG-TRX-OUT
           END-STRING
           WRITE REG-TRX-OUT.

       400-FINALIZE.
           CLOSE FILE-INPUT FILE-OUTPUT.
