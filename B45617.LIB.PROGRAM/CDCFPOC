       IDENTIFICATION DIVISION.
       PROGRAM-ID. CDCFPOC.
       AUTHOR.     MACDOM.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FILE-INPUT ASSIGN TO TXG01
               ORGANIZATION IS SEQUENTIAL
               FILE STATUS IS FS-TXG01.

           SELECT FILE-OUTPUT ASSIGN TO TXOUTG1
               ORGANIZATION IS SEQUENTIAL
               FILE STATUS IS FS-OUTG1.
       DATA DIVISION.
       FILE SECTION.
       FD  FILE-INPUT.
       01  REG-TRX.
           05  TS-INICIO         PIC X(26).   *> Fecha Hora Transacción
           05  CO-IDTM           PIC X(4).    *> Tipo Request
           05  CO-CONEX          PIC X(3).    *> Ruta
           05  CO-APPL           PIC X(3).    *> Aplicación
           05  FIS-TIPO          PIC X(4).    *> Tipo Response
           05  FIS-F2            PIC X(19).   *> Tarjeta
           05  FIS-F3            PIC X(6).    *> Processing Code
           05  FIS-F4            PIC X(12).   *> Monto Transacción
           05  FIS-F18           PIC X(4).    *> Merchant Type
           05  FIS-F22           PIC X(3).    *> POS Entry Mode
           05  FIS-F25           PIC X(2).    *> POS Condition Code
           05  FIS-F42           PIC X(15).   *> Código Comercio
           05  FIS-F43           PIC X(40).   *> Comercio
           05  FIS-F49           PIC X(3).    *> Moneda
           05  FIS-F124          PIC X(255).  *> Info
           05  CODUNICO          PIC X(10).   *> Código Único

       FD  FILE-OUTPUT.
       01  REG-TRX-OUT.
           05  TS-INICIO      PIC X(26).  *> Fecha Hora Transacción
           05  DELIM1         PIC X(2) .
           05  CO-IDTM        PIC X(4).   *> Tipo Request
           05  DELIM2         PIC X(2) .
           05  CO-CONEX       PIC X(3).   *> Ruta
           05  DELIM3         PIC X(2) .
           05  CO-APPL        PIC X(3).   *> Aplicación
           05  DELIM4         PIC X(2) .
           05  FIS-TIPO       PIC X(4).   *> Tipo Response
           05  DELIM5         PIC X(2) .
           05  FIS-F2         PIC X(19).  *> Tarjeta
           05  DELIM6         PIC X(2) .
           05  FIS-F3         PIC X(6).   *> Processing Code
           05  DELIM7         PIC X(2) .
           05  FIS-F4         PIC X(12).  *> Monto Transacción
           05  DELIM8         PIC X(2) .
           05  FIS-F18        PIC X(4).   *> Merchant Type
           05  DELIM9         PIC X(2) .
           05  FIS-F22        PIC X(3).   *> POS Entry Mode
           05  DELI10         PIC X(2) .
           05  FIS-F25        PIC X(2).   *> POS Condition Code
           05  DELI11         PIC X(2) .
           05  FIS-F42        PIC X(15).  *> Código Comercio
           05  DELI12         PIC X(2) .
           05  FIS-F43        PIC X(40).  *> Comercio
           05  DELI13         PIC X(2) .
           05  FIS-F49        PIC X(3).   *> Moneda
           05  DELI14         PIC X(2) .
           05  FIS-F124       PIC X(255). *> Info
           05  DELI15         PIC X(2) .
           05  CODUNICO       PIC X(10).  *> Código Único
           05  DELI16         PIC X(2) .
           05  CANAL          PIC X(20).  *> CANAL
           05  DELI17         PIC X(2) .
           05  MARCA          PIC X(10).  *> MARCA


       WORKING-STORAGE SECTION.
       01 FS-TXG01           PIC 99.
       01 FS-OUTG1           PIC 99.
       01 DELIM              PIC X(2) VALUE '|^'.

       01 SW-EOF.
         05 SW-EOF-END     PIC XX VALUE 'NO'.
           88 SW-EOF-OK VALUE 'SI'.
           88 SW-EOF-NO VALUE 'NO'.

       PROCEDURE DIVISION.
           PERFORM 100-START-PROGRAM
           PERFORM 200-PROCESS-TRXS
           PERFORM 400-FINALIZE
           STOP RUN
           .

       100-START-PROGRAM.
           INITIALIZE SW-EOF
           PERFORM OPEN-FILE-INPUT
           PERFORM OPEN-FILE-OUTPUT
           .

       200-PROCESS-TRXS.
           PERFORM READ-FILE-INPUT
           PERFORM 300-PROCESS-TRXS-RECORDS UNTIL SW-EOF-OK.

       300-PROCESS-TRXS-RECORDS.
           IF (
               (FIS-F2  OF REG-TRX(1:1)  = '3') OR
               (FIS-F2  OF REG-TRX(1:1)  = '4')
              ) AND
              (
               (FIS-TIPO  OF REG-TRX  = '0110') OR
               (FIS-TIPO  OF REG-TRX  = '0130') OR
               (FIS-TIPO  OF REG-TRX  = '0430')
              )

             PERFORM CALCULAR-CANAL
             PERFORM CALCULAR-MARCA
             PERFORM WRITE-OUTPUT
           END-IF

           PERFORM READ-FILE-INPUT
           .

       CALCULAR-CANAL.
           MOVE 'OTROS'  TO CANAL

           *> IMPORTE CERO
           IF FIS-F4 OF REG-TRX  = '000000000000'
              MOVE 'IMPORTE CERO' TO CANAL
           END-IF

           *> OTROS ATM
           IF (FIS-F18  OF REG-TRX  = '6010') OR
              (FIS-F18  OF REG-TRX  = '6011')
              MOVE 'OTROS ATM' TO CANAL
           END-IF

           *> ECOMMERCE 1
           IF (FIS-F25  OF REG-TRX  = '59') AND NOT
              (
               (FIS-F3  OF REG-TRX(1:2)  = '10') OR
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              )
              MOVE 'ECOMMERCE' TO CANAL
           END-IF

           *> ECOMMERCE 2
           IF (FIS-F25  OF REG-TRX  = '01') AND NOT
              (
               (FIS-F3  OF REG-TRX(1:2)  = '10') OR
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              ) AND
              (
               (FIS-F22  OF REG-TRX(1:2)  = '01') OR
               (FIS-F22  OF REG-TRX(1:2)  = '81')
              ) AND
              (FIS-F2  OF REG-TRX(1:1) = '4')
              MOVE 'ECOMMERCE' TO CANAL
           END-IF

           *> ECOMMERCE 3
           IF (FIS-F25  OF REG-TRX  = '01') AND NOT
              (
               (FIS-F3  OF REG-TRX(1:2)  = '10') OR
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              ) AND
              (FIS-F22  OF REG-TRX(1:2)  = '01') AND
              (FIS-F2  OF REG-TRX(1:1) = '3')
              MOVE 'ECOMMERCE' TO CANAL
           END-IF

           *> ECOMMERCE 4
           IF (FIS-F25  OF REG-TRX  = '00') AND NOT
              (
               (FIS-F3  OF REG-TRX(1:2)  = '10') OR
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              ) AND
              (FIS-F22  OF REG-TRX(1:2)  = '01') AND
              (FIS-F2  OF REG-TRX(1:1) = '4')
              MOVE 'ECOMMERCE' TO CANAL
           END-IF

           *> ECOMMERCE 5
           IF (FIS-F25  OF REG-TRX  = '00') AND NOT
              (
               (FIS-F3  OF REG-TRX(1:2)  = '10') OR
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              ) AND
              (
               (FIS-F22  OF REG-TRX(1:2)  = '01') OR
               (FIS-F22  OF REG-TRX(1:2)  = '06')
              ) AND
              (FIS-F2  OF REG-TRX(1:1) = '3')
              MOVE 'ECOMMERCE' TO CANAL
           END-IF

           *> ECOMMERCE 6
           IF (FIS-F25  OF REG-TRX  = '00') AND NOT
              (
               (FIS-F3  OF REG-TRX(1:2)  = '10') OR
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              ) AND
              (FIS-F22  OF REG-TRX(1:2)  = '81') AND
              (FIS-F2   OF REG-TRX(1:1) = '3') AND NOT
              (FIS-F124 OF REG-TRX(1:4) = 'T115')
              MOVE 'ECOMMERCE' TO CANAL
           END-IF

           *> ECOMMERCE 7
           IF (FIS-F25  OF REG-TRX  = '00') AND NOT
              (
               (FIS-F3  OF REG-TRX(1:2)  = '10') OR
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              ) AND
              (FIS-F22  OF REG-TRX(1:2)  = SPACES) AND
              (FIS-F18  OF REG-TRX       = '4121') AND
              (FIS-F2  OF  REG-TRX(1:1)   = '3') AND NOT
              (FIS-F124 OF REG-TRX(1:4) = 'T115')
              MOVE 'ECOMMERCE' TO CANAL
           END-IF

           *> VISA DIRECT OCT
           IF (
               (FIS-F25  OF REG-TRX  = '00') OR
               (FIS-F25  OF REG-TRX  = '01') OR
               (FIS-F25  OF REG-TRX  = '05') OR
               (FIS-F25  OF REG-TRX  = '59')
              ) AND
              (
               (FIS-F3  OF REG-TRX(1:2)  = '55') OR
               (FIS-F3  OF REG-TRX(1:2)  = '56') OR
               (FIS-F3  OF REG-TRX(1:2)  = '70')
              )
              MOVE 'VISA DIRECT OCT' TO CANAL
           END-IF

           *> VISA DIRECT AFT
           IF (
               (FIS-F25  OF REG-TRX  = '00') OR
               (FIS-F25  OF REG-TRX  = '01') OR
               (FIS-F25  OF REG-TRX  = '05') OR
               (FIS-F25  OF REG-TRX  = '59')
              ) AND
              (FIS-F3  OF REG-TRX(1:2)  = '10')
              MOVE 'VISA DIRECT AFT' TO CANAL
           END-IF

           *> RECURRENTE
           IF (FIS-F25  OF REG-TRX  = '40')
              MOVE 'RECURRENTE' TO CANAL
           END-IF

           *> MOTO
           IF (FIS-F25  OF REG-TRX  = '08')
              MOVE 'MOTO' TO CANAL
           END-IF

           *> POS1
           IF (FIS-F25  OF REG-TRX  = '00') AND
              (
               (FIS-F22  OF REG-TRX(1:2)  = '02') OR
               (FIS-F22  OF REG-TRX(1:2)  = '05') OR
               (FIS-F22  OF REG-TRX(1:2)  = '07')
              )
              MOVE 'POS' TO CANAL
           END-IF

           *> POS2
           IF (FIS-F25  OF REG-TRX  = '00') AND
              (FIS-F22  OF REG-TRX(1:2)  = '81') AND
              (FIS-F124 OF REG-TRX(1:4)  = 'T115') AND
              (FIS-F2   OF REG-TRX(1:1)  = '3')
              MOVE 'POS' TO CANAL
           END-IF

           *> POS3
           IF (FIS-F25  OF REG-TRX  = '02') AND
              (
               (FIS-F22  OF REG-TRX(1:2)  = '05') OR
               (FIS-F22  OF REG-TRX(1:2)  = '07')
              )
              MOVE 'POS' TO CANAL
           END-IF
           .

       CALCULAR-MARCA.
           MOVE SPACES  TO MARCA

           *> AMEX
           IF (FIS-F2  OF REG-TRX(1:1)  = '3')
               MOVE 'AMEX' TO MARCA
           END-IF

           *> VISA
           IF (FIS-F2  OF REG-TRX(1:1)  = '4')
               MOVE 'VISA' TO MARCA
           END-IF
           .

       OPEN-FILE-INPUT.
           OPEN INPUT FILE-INPUT
           IF (FS-TXG01  > "07")
            DISPLAY "ERR ABRIR ARCH ENTRADA" FS-TXG01
            STOP RUN
           END-IF.

       READ-FILE-INPUT.
           READ FILE-INPUT
           IF (FS-TXG01  = "10")
            SET SW-EOF-OK TO TRUE
           END-IF
           .

       OPEN-FILE-OUTPUT.
           OPEN OUTPUT  FILE-OUTPUT
           IF (FS-OUTG1   > "07")
            DISPLAY "ERR ABRIR ARCH SALIDA" FS-OUTG1
            STOP RUN
           END-IF.

       WRITE-OUTPUT.
           MOVE CORRESPONDING REG-TRX TO REG-TRX-OUT

           MOVE DELIM TO DELIM1
           MOVE DELIM TO DELIM2
           MOVE DELIM TO DELIM3
           MOVE DELIM TO DELIM4
           MOVE DELIM TO DELIM5
           MOVE DELIM TO DELIM6
           MOVE DELIM TO DELIM7
           MOVE DELIM TO DELIM8
           MOVE DELIM TO DELIM9
           MOVE DELIM TO DELI10
           MOVE DELIM TO DELI11
           MOVE DELIM TO DELI12
           MOVE DELIM TO DELI13
           MOVE DELIM TO DELI14
           MOVE DELIM TO DELI15
           MOVE DELIM TO DELI16
           MOVE DELIM TO DELI17

           WRITE REG-TRX-OUT
           .



       400-FINALIZE.
           CLOSE FILE-INPUT FILE-OUTPUT.

       END PROGRAM CDCCFPG1.
